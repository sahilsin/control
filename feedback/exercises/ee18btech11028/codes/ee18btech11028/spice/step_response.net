OP1.CIR - OPAMP CLOSED-LOOP STEP RESPONSE
* STEP INPUT
VS	7	0	AC	1	PWL(0US 0V   0.1US 1V   10US 1V)
*
R1	1	7	100K
CF	2	0	10000PF
R2	2	4	100K
XOP	1 2	4	OPAMP1	
*
* SINGLE-POLE OPERATIONAL AMPLIFIER MACRO-MODEL
* connections:      non-inverting input
*                   |   inverting input
*                   |   |   output
*                   |   |   |
.SUBCKT OPAMP1	     1   2   6
* INPUT IMPEDANCE
RIN	1	2	100MEG
* DC GAIN (100K) AND POLE 1 (10/2*pi HZ)
EGAIN	3 0	1 2	100K
RP1	3	4	1K
CP1	4	0	100UF
* OUTPUT BUFFER AND RESISTANCE
EBUFFER	5 0	4 0	1
ROUT	5	6	10
.ENDS
*
* ANALYSIS
*.AC 	DEC 	100 10 10MEG
.TRAN 	0.0001S  0.015S
*
* VIEW RESULTS
.control
run
*.PRINT	AC 	VM(4)
*.PLOT AC	VM(4)
*.PLOT	TRAN 	V(1) V(4)
*.PRINT	TRAN 	V(1) V(4)
*.PROBE
wrdata ee18btech11028_sim.dat V(4)
.endc
.end
